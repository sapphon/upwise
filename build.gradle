buildscript {
	ext {
		environment = 'local'	/* alternative is 'fordnetwork'*/
		springBootVersion = '2.0.3.RELEASE'
	}
	if(environment == 'fordnetwork'){
		System.setProperty('http.proxyHost', 'proxyvipfmcc.nb.ford.com')
		System.setProperty('http.proxyPort', '83')
		System.setProperty('http.nonProxyHosts', '*.ford.com|localhost')
		System.setProperty('https.proxyHost', 'proxyvipfmcc.nb.ford.com')
		System.setProperty('https.proxyPort', '83')
		System.setProperty('https.nonProxyHosts', '*.ford.com|localhost')
	}
	repositories {
		mavenCentral()
		maven { url "https://oss.sonatype.org/content/repositories/releases" }
		if(environment == 'fordnetwork') {
			maven { url "http://www.nexus.ford.com/repository/external-proxy-group/" }
			maven { url "https://wwwqa.nexus.ford.com/repository/ford_community/" }
		}
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("com.github.pivotalservices:ya-cf-app-gradle-plugin:2.3.0-rc.1")
		if(environment == 'fordnetwork') {
			classpath("com.ford.quartermaster:quartermaster-plugin:1.1-SNAPSHOT")
		}
	}
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
if(environment == 'fordnetwork'){
	apply plugin: 'quartermaster-plugin'
}


group = 'org.sapphon'
version = '0.0.1-SNAPSHOT'
archivesBaseName = 'upwise'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
	if(environment == 'fordnetwork') {
		maven { url "http://www.nexus.ford.com/repository/external-proxy-group/" }
		maven { url "http://www.nexus.ford.com/content/groups/public/" }
	}
}


dependencies {
	compile('javax.xml.bind:jaxb-api:2.3.0')
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-starter-security')
	compile('org.springframework.security:spring-security-test')
	compile('org.springframework.boot:spring-boot-starter-web')
	compile('io.springfox:springfox-swagger-ui:2.6.1')
	compile('io.springfox:springfox-swagger2:2.6.1')
	compile('org.apache.commons:commons-lang3:3.6')
	compile('org.springframework.session:spring-session-core')
	compile('com.h2database:h2')

	//dependencies used for rendering only
	compile('org.thymeleaf:thymeleaf-spring5:3.0.9.RELEASE')
	compile('org.thymeleaf:thymeleaf:3.0.9.RELEASE')
	compile('org.thymeleaf.extras:thymeleaf-extras-springsecurity4')

	//runtimes
	runtime('org.springframework.boot:spring-boot-devtools')
	runtime('mysql:mysql-connector-java')

	//test stuff
	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile('io.rest-assured:rest-assured:3.0.7')
	testCompile('org.springframework.restdocs:spring-restdocs-mockmvc')
}

apply plugin: 'cf-app'
cfConfig {
	ccHost = "api.run.pivotal.io" // Change
	space = "prod" // Change
	org = "upwise" // Change
	ccUser = "user"
	ccPassword = "pass"
	name = archivesBaseName
	host = archivesBaseName
	domain = "upwise.cfapps.io" // Change
	memory = 512
	instances = 1
	buildpack = "java_buildpack"
	filePath = "build/libs/" + archivesBaseName + "-" + version + ".jar"
}
